# Maintainer: X0rg

pkgname=man-pages-fr
pkgver=3.65
pkgrel=2
pkgdesc="Pages de manuel Linux francophones de 'Traduc.org'"
arch=('any')
url="http://traduc.org/perkamon/"
license=('custom')
group=('manpagesfr')
optdepends=('manpages-fr-extra')
options=(docs)
install=$pkgname.install
changelog=ChangeLog
source=(http://perkamon.alioth.debian.org/archives/$pkgver/$pkgname-$pkgver-1.tar.xz)
#source=(https://alioth.debian.org/frs/download.php/latestfile/967/$pkgname-$pkgver-1.tar.xz)
md5sums=('c6b68d4bd55965696101f99b1c23611a')

prepare() {
	mv -v "$srcdir/fr/README.fr" "$srcdir"
}

build() {
	cd "$srcdir/fr"

	msg2 "Renomme certaines pages de manuel françaises pour éviter des conflits..."
	fichiersignores=$(pacman -Qql $pkgname 2> /dev/null | grep ".gz" | cut -d/ -f6-)
	for fichierteste in man*/*; do
		( [[ -f /usr/share/man/fr/$fichierteste.gz ]] && ! echo $fichiersignores | grep -qF $fichierteste ) && \
		( mv -v $fichierteste $(echo $fichierteste | sed "s|\.|2.|") || return 1 )
	done

	true
}

package() {
	msg2 "Mise en place des pages de manuel francophones..."
	mkdir -p "$pkgdir/usr/share/man/"
	mv -v "$srcdir/fr" "$pkgdir/usr/share/man/"

	msg2 "Installe la licence..."
	install -Dvm644 "$srcdir/README.fr" "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
