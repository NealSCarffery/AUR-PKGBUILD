# Maintainer: X0rg

_pkgbasename=memtest86
pkgname=$_pkgbasename-efi
pkgver=5.0.0
pkgrel=1
pkgdesc="A free, thorough, stand alone memory test as an EFI application."
arch=('i686' 'x86_64')
url="http://www.memtest86.com"
license=('GPL2')
makedepends=('p7zip')
install=$pkgname.install
source=("$_pkgbasename-$pkgver".iso.gz::"$url/downloads/$_pkgbasename-iso.gz"
	"$pkgname"
	"$pkgname.conf")
md5sums=('483b1066de1f631b3d4222b95bcbfb5e'
         'd4e15fe577ff95bc3317212cb719bdde'
         '488ef0f904318a7fce4b7a292597019b')

prepare() {
	7z x -y $_pkgbasename-$pkgver.iso

	cd "$srcdir/EFI/BOOT/"
	mv MT86.PNG mt86.png
	[[ "${CARCH}" == "i686" ]]   && rm BOOTX64.EFI	&& mv BOOTIA32.EFI bootia32.efi
	[[ "${CARCH}" == "x86_64" ]] && rm BOOTIA32.EFI	&& mv BOOTX64.EFI bootx64.efi
}

package() {
	install -dv "$pkgdir/usr/share/$pkgname"
	install -Dvm755 "$srcdir/EFI/BOOT/"boot*.efi "$pkgdir/usr/share/$pkgname"
	install -Dvm644 "$srcdir/EFI/BOOT/mt86.png" "$pkgdir/usr/share/$pkgname"

	install -dv "$pkgdir/usr/bin"
	install -Dvm755 "$srcdir/$pkgname" "$pkgdir/usr/bin/$pkgname"

	install -dv "$pkgdir/etc"
	install -Dvm644 "$srcdir/$pkgname.conf" "$pkgdir/etc/$pkgname.conf"
}
