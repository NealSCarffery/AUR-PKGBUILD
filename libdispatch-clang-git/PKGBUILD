# Maintainer: X0rg

_gitname=libdispatch
pkgname=$_gitname-clang-git
pkgver=269.8e44de9
pkgrel=1
pkgdesc="Linux port of Apple's open-source concurrency library with blocks support, using Clang"
arch=('i686' 'x86_64')
url="http://nickhutchinson.me/libdispatch"
license=('Apache')
depends=('libkqueue' 'libpthread_workqueue' 'gnustep-libobjc2-clang-svn')
makedepends=('git' 'cmake' 'gnustep-base-clang-svn' 'gnustep-corebase-clang-svn')
conflicts=('libdispatch-svn')
source=(git://github.com/nickhutchinson/$_gitname.git
	https://raw.githubusercontent.com/LubosD/darling-overlay/master/dev-libs/$_gitname/files/gnustep-blocks.patch)
md5sums=('SKIP'
         'ecf2b7a3301d8ebe966ac6662b666459')

pkgver() {
  cd "$srcdir/$_gitname"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/$_gitname"
  msg2 "Patch to use GNUstep Libobjc2 Blocks..."
  sed -i "s/-l:libobjc.so.4/-l:libobjc.so.4.6/g" "$srcdir/gnustep-blocks.patch"
  patch -p1 -i "$srcdir/gnustep-blocks.patch"

  msg2 "Make 'build' directory..."
  mkdir -pv ./build
}

build() {
  cd "$srcdir/$_gitname/build"
  msg2 "Run 'cmake'..."
  OBJCFLAGS="-fblocks" CC="clang" CXX="clang++" cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release

  msg2 "Run 'make'..."
  make
}

package() {
  cd "$srcdir/$_gitname/build"
  msg2 "Install..."
  make DESTDIR="$pkgdir" install
}
