# Maintainer: X0rg

_svnname=gnustep-gui
pkgname=$_svnname-clang-svn
pkgver=37873
pkgrel=7
pkgdesc="The GNUstep GUI class library, using Clang"
arch=('i686' 'x86_64')
url="http://www.gnustep.org/"
license=('GPL3' 'LGPL2.1')
groups=('gnustep-clang-svn')
depends=('libtiff' 'libjpeg-turbo' 'libpng' 'gnustep-base-clang-svn')
makedepends=('svn' 'clang' 'gnustep-make-clang-svn')
optdepends=('giflib: enable libgif-based GIF support'
	'aspell: enable aspell for spellchecker'
	'flite: enable speech server'
	'libsndfile: enable sound (need libao to work)'
	'libao: enable sound (need libsndfile to work)'
	'libcups: enable cups printing support')
conflicts=('gnustep-gui' 'gnustep-gui-svn')
options=('!emptydirs')
source=("$_svnname::svn://svn.gna.org/svn/gnustep/libs/gui/trunk/")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_svnname"
  svnversion | tr -d [A-z]
}

prepare() {
  msg2 "Patch 'NSBitmapImageRep+PNG.m' file..."
  sed -i 's|png_sizeof|sizeof|g' "$srcdir/$_svnname/Source/NSBitmapImageRep+PNG.m"
}

build() {
  cd "$srcdir/$_svnname"
  msg2 "Run 'configure'..."
  OBJCFLAGS="-fblocks" CC="clang" CXX="clang++" ./configure --prefix=/usr --sysconfdir=/etc/GNUstep --enable-libgif

  msg2 "Run 'make'..."
  make
}

# check() {
#   cd "$srcdir/$_svnname"
#   make check
# }

package() {
  cd "$srcdir/$_svnname"
  msg2 "Install..."
  make DESTDIR="$pkgdir" install
}
