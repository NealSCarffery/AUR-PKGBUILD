# Maintainer: X0rg

_svnname=gnustep-gui
pkgname=$_svnname-clang-svn
pkgver=37850
pkgrel=6
pkgdesc="The GNUstep GUI class library, using Clang"
arch=('i686' 'x86_64')
url="http://www.gnustep.org/"
license=('GPL3' 'LGPL3')
groups=('gnustep-clang-svn')
depends=('libtiff' 'libjpeg-turbo' 'libpng' 'giflib' 'libcups' 'aspell' 'libao' 'libsndfile' 'gnustep-base-clang-svn')
makedepends=('svn' 'clang' 'gnustep-make-clang-svn' 'audiofile' 'portaudio>=19')
conflicts=('gnustep-gui' 'gnustep-gui-svn')
options=('!emptydirs')
source=("$_svnname::svn://svn.gna.org/svn/gnustep/libs/gui/trunk/")
md5sums=('SKIP')

pkgver() {
  cd "$_svnname"
  svnversion | tr -d [A-z]
}

prepare() {
  cd "$_svnname"
  msg2 "Patch 'NSBitmapImageRep+PNG.m' file..."
  sed -i 's|png_sizeof|sizeof|g' Source/NSBitmapImageRep+PNG.m
}

build() {
  cd "$_svnname"
  msg2 "Run 'configure'..."
  OBJCFLAGS="-fblocks" CC="clang" CXX="clang++" ./configure --prefix=/usr --sysconfdir=/etc/GNUstep --enable-libgif

  msg2 "Run 'make'..."
  make
}

# check() {
#   cd "$_svnname"
#   make check
# }

package() {
  cd "$_svnname"
  msg2 "Install..."
  make DESTDIR="$pkgdir" install
}
