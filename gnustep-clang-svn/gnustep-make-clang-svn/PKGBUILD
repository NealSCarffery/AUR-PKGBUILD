# Maintainer: X0rg

_svnname=gnustep-make
pkgname=$_svnname-clang-svn
pkgver=37850
pkgrel=4
pkgdesc="The GNUstep make package, using Clang"
arch=('any')
url="http://www.gnustep.org/"
license=('GPL3')
groups=('gnustep-clang-svn')
depends=('llvm')
makedepends=('svn' 'clang')
conflicts=('gnustep-make')
options=('!emptydirs')
source=("$_svnname::svn://svn.gna.org/svn/gnustep/tools/make/trunk/"
	'arch')
md5sums=('SKIP'
         '0daa9a592d808306193540bda0980673')

pkgver() {
  cd "$_svnname"
  svnversion | tr -d [A-z]
}

prepare() {
  msg2 "Copy new file system layouts for Arch..."
  cp -v "$srcdir/arch" "$_svnname/FilesystemLayouts/"
}

build() {
  cd "$_svnname"
  msg2 "Run 'configure'..."
  CC="clang" CXX="clang++" ./configure --prefix=/usr --sysconfdir=/etc/GNUstep --with-layout=arch
}

package() {
  cd "$_svnname"
  msg2 "Install..."
  make DESTDIR="$pkgdir" install
  echo -e "# Added by $pkgname package\nexport PATH=$PATH" >> "$pkgdir/usr/share/GNUstep/Makefiles/GNUstep-reset.sh"
}
