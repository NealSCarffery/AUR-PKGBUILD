# Maintainer: X0rg

_svnname=gnustep-make
pkgname=$_svnname-clang-svn
pkgver=37873
pkgrel=5
pkgdesc="The GNUstep make package, using Clang"
arch=('any')
url="http://www.gnustep.org/"
license=('GPL3')
groups=('gnustep-clang-svn')
depends=('bash' 'gcc-libs>=4.9.0') # Clang needs symbol 'GLIBCXX_3.4.20' in /usr/lib/libstdc++.so.6
makedepends=('svn' 'clang')
conflicts=('gnustep-make')
options=('!emptydirs')
source=("$_svnname::svn://svn.gna.org/svn/gnustep/tools/make/trunk/"
	'arch')
md5sums=('SKIP'
         '0daa9a592d808306193540bda0980673')

pkgver() {
  cd "$srcdir/$_svnname"
  svnversion | tr -d [A-z]
}

prepare() {
  msg2 "Copy new file system layouts for Arch..."
  cp -v "$srcdir/arch" "$srcdir/$_svnname/FilesystemLayouts/"
}

build() {
  cd "$srcdir/$_svnname"
  msg2 "Run 'configure'..."
  CC="clang" CXX="clang++" ./configure --prefix=/usr --sysconfdir=/etc/GNUstep --with-layout=arch --with-objc-lib-flag=-l:libobjc.so.4.6
}

package() {
  cd "$srcdir/$_svnname"
  msg2 "Install..."
  make DESTDIR="$pkgdir" install
  echo -e "# Added by $pkgname package\nexport PATH=$PATH" >> "$pkgdir/usr/share/GNUstep/Makefiles/GNUstep-reset.sh"
}
