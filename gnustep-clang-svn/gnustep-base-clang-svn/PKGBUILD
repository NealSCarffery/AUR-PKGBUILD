# Maintainer: X0rg

_svnname=gnustep-base
pkgname=$_svnname-clang-svn
pkgver=37850
pkgrel=4
pkgdesc="The GNUstep base package, using Clang"
arch=('i686' 'x86_64')
url="http://www.gnustep.org/"
license=('GPL2' 'GPL3' 'LGPL3')
groups=('gnustep-clang-svn')
depends=('libffi' 'libxml2' 'libxslt' 'openssl' 'gnutls' 'icu' 'avahi' 'xz' 'gnustep-libobjc2-clang-svn')
makedepends=('svn' 'clang' 'gnustep-make-clang-svn')
optdepends=('iconv: only if you don`t have glibc'
	'ffcall: alternative for libffi')
conflicts=('gnustep-base' 'gnustep-base-svn')
options=('!emptydirs')
source=("$_svnname::svn://svn.gna.org/svn/gnustep/libs/base/trunk/")
md5sums=('SKIP')

pkgver() {
  cd "$_svnname"
  svnversion | tr -d [A-z]
}

build() {
  cd "$_svnname"

  msg2 "Run 'configure'..."
  OBJCFLAGS="-fblocks" CC="clang" CXX="clang++" LDFLAGS="$LDFLAGS,-l:libobjc.so.4.6" ./configure --prefix=/usr --sysconfdir=/etc/GNUstep \
    --disable-unicodeconstants --with-ffi-include=/usr/lib/libffi-`pacman -Q libffi | cut -f2 -d\ |cut -f1 -d-`/include/

  msg2 "Fix permissions..."
  sed -i 's/tar -xf $(TIMEZONE_ARCHIVE);/tar -xf $(TIMEZONE_ARCHIVE);chown -R root:root * ;/' NSTimeZones/Makefile.postamble

  msg2 "Run 'make'..."
  make
}

# check() {
#   cd "$_svnname"
#   make check
# }

package() {
  cd "$_svnname"
  msg2 "Install..."
  make DESTDIR="$pkgdir" install
}
