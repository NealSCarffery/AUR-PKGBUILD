# Maintainer: X0rg

_svnname=gnustep-base
pkgname=$_svnname-clang-svn
pkgver=37873
pkgrel=5
pkgdesc="The GNUstep base package, using Clang"
arch=('i686' 'x86_64')
url="http://www.gnustep.org/"
license=('GPL2' 'GPL3' 'LGPL2.1')
groups=('gnustep-clang-svn')
depends=('libffi' 'libxml2' 'libxslt' 'openssl' 'gnutls' 'icu' 'gnustep-libobjc2-clang-svn')
makedepends=('svn' 'clang' 'gnustep-make-clang-svn')
optdepends=('iconv: only if you don`t have glibc'
	'ffcall: alternative for libffi'
	'avahi: enable NSNetServices support'
	'libdispatch-clang-git: enable dispatching blocks via libdispatch')
conflicts=('gnustep-base' 'gnustep-base-svn')
options=('!emptydirs')
source=("$_svnname::svn://svn.gna.org/svn/gnustep/libs/base/trunk/")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_svnname"
  svnversion | tr -d [A-z]
}

prepare() {
  msg2 "Fix permissions..."
  sed -i 's/tar -xf $(TIMEZONE_ARCHIVE);/tar -xf $(TIMEZONE_ARCHIVE);chown -R root:root * ;/' "$srcdir/$_svnname/NSTimeZones/Makefile.postamble"
}

build() {
  cd "$srcdir/$_svnname"

  msg2 "Run 'configure'..."
  OBJCFLAGS="-fblocks" CC="clang" CXX="clang++" ./configure --prefix=/usr --sysconfdir=/etc/GNUstep \
    --disable-unicodeconstants --with-ffi-include=/usr/lib/libffi-`pacman -Q libffi | cut -f2 -d\ |cut -f1 -d-`/include/

  msg2 "Run 'make'..."
  make
}

# check() {
#   cd "$srcdir/$_svnname"
#   make check
# }

package() {
  cd "$srcdir/$_svnname"
  msg2 "Install..."
  make DESTDIR="$pkgdir" install
}
