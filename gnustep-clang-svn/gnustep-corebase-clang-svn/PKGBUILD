# Maintainer: X0rg

_svnname=gnustep-corebase
pkgname=$_svnname-clang-svn
pkgver=38274
pkgrel=7
pkgdesc="The GNUstep CoreBase Library is a library of general-purpose, non-graphical C objects, using Clang."
arch=('i686' 'x86_64')
url="http://www.gnustep.org/"
license=('GPL3' 'LGPL2.1')
groups=('gnustep-clang-svn')
depends=('gnustep-base-clang-svn' 'libdispatch-clang-git')
makedepends=('svn' 'clang' 'gnustep-make-clang-svn')
conflicts=('gnustep-corebase-git')
options=('!emptydirs')
source=("$_svnname::svn://svn.gna.org/svn/gnustep/libs/corebase/trunk/")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_svnname"
  svnversion | tr -d [A-z]
}

prepare() {
  msg2 "Fix build error..."
  sed -i -e 's|#error 96-bit long double currently not supported!|//#error 96-bit long double currently not supported!|g' "$srcdir/$_svnname/Source/GSUnicode.c"
}

build() {
  cd "$srcdir/$_svnname"
  msg2 "Run 'configure'..."
  OBJCFLAGS="-fblocks" CC="clang" CXX="clang++" ./configure --prefix=/usr --sysconfdir=/etc/GNUstep

  msg2 "Run 'make'..."
  make
}

# check() {
#   cd "$srcdir/$_svnname"
#   make check
# }

package() {
  cd "$srcdir/$_svnname"
  msg2 "Install..."
  make DESTDIR="$pkgdir" install
}
