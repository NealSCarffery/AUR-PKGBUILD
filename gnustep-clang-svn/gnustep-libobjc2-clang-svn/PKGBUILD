# Maintainer: X0rg

_svnname=gnustep-libobjc2
pkgname=$_svnname-clang-svn
pkgver=37873
pkgrel=4
pkgdesc="The GNUstep Objective-C runtime is designed as a drop-in replacement for the GCC runtime, using Clang"
arch=('i686' 'x86_64')
url="http://www.gnustep.org/"
license=('custom:MIT')
groups=('gnustep-clang-svn')
depends=('gcc-libs')
makedepends=('svn' 'clang' 'cmake' 'gnustep-make-clang-svn')
conflicts=('gnustep-libobjc2-git')
options=('purge')
source=("$_svnname::svn://svn.gna.org/svn/gnustep/libs/libobjc2/trunk/")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_svnname"
  svnversion | tr -d [A-z]
}

prepare() {
  msg2 "Create 'build' directory..."
  mkdir -pv "$srcdir/$_svnname/build"

  msg2 "Delete 'GNUmakefile' file..."
  rm -v "$srcdir/$_svnname/GNUmakefile"
}

build() {
  cd "$srcdir/$_svnname/build"
  msg2 "Run 'cmake'..."
  OBJCFLAGS="-fblocks" CC="clang" CXX="clang++" LDFLAGS="${LDFLAGS//,--as-needed}" cmake ..

  msg2 "Run 'make'..."
  make
}

# check() {
#   cd "$srcdir/$_svnname/build"
#   make test
# }

package() {
  cd "$srcdir/$_svnname/build"
  msg2 "Install..."
  make DESTDIR="$pkgdir" install
  PURGE_TARGETS="$pkgdir/usr/lib/libobjc.so"

  msg2 "Install the license..."
  install -Dvm644 "../COPYING" "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
