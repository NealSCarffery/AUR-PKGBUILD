# Maintainer: X0rg

_svnname=gnustep-libobjc2
pkgname=$_svnname-clang-svn
pkgver=37850
pkgrel=4
pkgdesc="The GNUstep Objective-C runtime is designed as a drop-in replacement for the GCC runtime, using Clang"
arch=('i686' 'x86_64')
url="http://www.gnustep.org/"
license=('custom')
groups=('gnustep-clang-svn')
depends=('gcc-libs')
makedepends=('svn' 'clang' 'cmake' 'gnustep-make-clang-svn')
conflicts=('gnustep-libobjc2-git')
source=("$_svnname::svn://svn.gna.org/svn/gnustep/libs/libobjc2/trunk/")
md5sums=('SKIP')

pkgver() {
  cd "$_svnname"
  svnversion | tr -d [A-z]
}

prepare() {
  cd "$_svnname"
  msg2 "Create 'build' directory..."
  mkdir -pv ./build

  msg2 "Delete 'GNUmakefile' file..."
  rm -v ./GNUmakefile
}

build() {
  cd "$_svnname/build"
  msg2 "Run 'cmake'..."
  OBJCFLAGS="-fblocks" CC="clang" CXX="clang++" LDFLAGS="${LDFLAGS//,--as-needed}" cmake ..

  msg2 "Run 'make'..."
  make
}

# check() {
#   cd "$_svnname/build"
#   make test
# }

package() {
  cd "$_svnname/build"
  msg2 "Install..."
  make DESTDIR="$pkgdir" install
  rm "$pkgdir/usr/lib/libobjc.so"

  cd ..
  msg2 "Install the license..."
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
