# Maintainer: X0rg
# Contributor: Yen Chi Hsuan <yan12125 at gmail.com>
# Contributor: Nicolas Qui√©not < niQo at aur >
# Contributor: Jens Staal <staal1978@gmail.com>

_realname=libpthread_workqueue-git
pkgname=lib32-$_realname
pkgver=20140727
pkgrel=1
pkgdesc="a portable implementation of the pthread_workqueue API first introduced in Mac OS X (32-bit)."
url="https://github.com/mheily/libpwq"
arch=('i686' 'x86_64')
license=('custom')
depends=('lib32-glibc' 'lib32-libkqueue' 'libpthread_workqueue-git')
makedepends=('git' 'pkgconfig')
provides=('libpthread_workqueue' 'libpthread_workqueue-libpthread')
source=("${pkgname}"::'git+https://github.com/mheily/libpwq')
md5sums=('SKIP')

pkgver() {
    cd "${srcdir}/${pkgname}"
    git log -1 --format='%cd' --date=short | tr -d -- '-'
}

build() {
    export CC="gcc -m32"
    export CXX="g++ -m32"
    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

    cd "${srcdir}/${pkgname}"

    autoreconf --install --symlink --verbose
    ./configure --prefix=/usr --libdir=/usr/lib32

    make
}

package() {
    cd "${srcdir}/${pkgname}"
    make DESTDIR=$pkgdir install

    # Clean up lib32 package
    rm -rf "$pkgdir"/usr/{include,share}
}
