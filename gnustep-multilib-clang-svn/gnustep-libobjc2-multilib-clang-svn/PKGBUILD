# Maintainer: X0rg

_svnname=gnustep-libobjc2
pkgname=$_svnname-multilib-clang-svn
pkgver=37852
pkgrel=3
pkgdesc="The GNUstep Objective-C runtime is designed as a drop-in replacement for the GCC runtime for multilib, using Clang"
arch=('x86_64')
url="http://www.gnustep.org/"
license=('custom')
groups=('gnustep-multilib-clang-svn')
depends=('gcc-libs' 'lib32-gcc-libs')
makedepends=('svn' 'clang' 'cmake' 'gnustep-make-multilib-clang-svn')
provide=('gnustep-libobjc2-clang-svn')
conflicts=('gnustep-libobjc2-git' 'gnustep-libobjc2-clang-svn')
options=('purge')
source=("$_svnname::svn://svn.gna.org/svn/gnustep/libs/libobjc2/trunk/")
md5sums=('SKIP')

pkgver() {
  cd "$_svnname"
  svnversion | tr -d [A-z]
}

prepare() {
  cd "$_svnname"
  msg2 "Create 'build' directory..."
  mkdir -pv ./build32 ./build64

  msg2 "Delete 'GNUmakefile' file..."
  rm -v ./GNUmakefile
}

build() {
  # 64-bit build
  cd "$_svnname/build64"
  msg2 "Run 'cmake' (64-bit)..."
  OBJCFLAGS="-fblocks" CC="clang" CXX="clang++" LDFLAGS="${LDFLAGS//,--as-needed}" cmake ..

  msg2 "Run 'make' (64-bit)..."
  make

  # 32-bit build
  cd "../build32"
  source "/usr/share/GNUstep32/Makefiles/GNUstep.sh"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  msg2 "Run 'cmake' (32-bit)..."
  OBJCFLAGS="-fblocks" ASMFLAGS="-m32" CC="clang -m32" CXX="clang++ -m32" LDFLAGS="${LDFLAGS//,--as-needed}" cmake ..

  msg2 "Run 'make' (32-bit)..."
  make
}

# check() {
#   cd "$_svnname/build64"
#   make test
#
#   cd "../build32"
#   make test
# }

package() {
  # 64-bit install
  cd "$_svnname/build64"
  msg2 "Install (64-bit)..."
  make DESTDIR="$pkgdir" install

  # 32-bit install
  cd "../build32"
  msg2 "Install (32-bit)..."
  make DESTDIR="$pkgdir" install
  PURGE_TARGETS="$pkgdir/usr/lib*/libobjc.so"

  msg2 "Install the license..."
  install -Dvm644 ../COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
