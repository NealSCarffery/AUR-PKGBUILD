# Maintainer: X0rg

_svnname=gnustep-base
pkgname=$_svnname-multilib-clang-svn
pkgver=37852
pkgrel=3
pkgdesc="The GNUstep base package for multilib, using Clang"
arch=('x86_64')
url="http://www.gnustep.org/"
license=('GPL2' 'GPL3' 'LGPL3')
groups=('gnustep-multilib-clang-svn')
depends=('libffi'      'libxml2'       'libxslt'       'openssl'       'gnutls'       'icu'       'avahi'       'xz' \
         'lib32-libffi' 'lib32-libxml2' 'lib32-libxslt' 'lib32-openssl' 'lib32-gnutls' 'lib32-icu' 'lib32-avahi' 'lib32-xz' 'gnustep-libobjc2-multilib-clang-svn')
makedepends=('svn' 'clang' 'gnustep-make-multilib-clang-svn' 'lib32-gmp' 'lib32-libgcrypt' 'lib32-zlib' 'lib32-glibc')
optdepends=('iconv: only if you don`t have glibc'
	'ffcall: alternative for libffi')
provide=('gnustep-base-clang-svn')
conflicts=('gnustep-base' 'gnustep-base-svn' 'gnustep-base-clang-svn')
options=('!emptydirs')
source=("$_svnname::svn://svn.gna.org/svn/gnustep/libs/base/trunk/")
md5sums=('SKIP')

pkgver() {
  cd "$_svnname"
  svnversion | tr -d [A-z]
}

prepare() {
  msg2 "Make a clone of $_svnname"
  cp -Rv "$_svnname" "$_svnname-32"
}

build() {
  # 64-bit build
  cd "$_svnname"

  msg2 "Run 'configure' (64-bit)..."
  OBJCFLAGS="-fblocks" CC="clang" CXX="clang++" LDFLAGS="$LDFLAGS,-l:libobjc.so.4.6" ./configure --prefix=/usr --sysconfdir=/etc/GNUstep \
    --disable-unicodeconstants --with-ffi-include=/usr/lib/libffi-`pacman -Q libffi | cut -f2 -d\ |cut -f1 -d-`/include/

  msg2 "Fix permissions (64-bit)..."
  sed -i 's/tar -xf $(TIMEZONE_ARCHIVE);/tar -xf $(TIMEZONE_ARCHIVE);chown -R root:root * ;/' NSTimeZones/Makefile.postamble

  msg2 "Run 'make' (64-bit)..."
  make

  # 32-bit build
  cd "../$_svnname-32"
  source "/usr/share/GNUstep32/Makefiles/GNUstep.sh"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  msg2 "Run 'configure' (32-bit)..."
  OBJCFLAGS="-fblocks" CC="clang -m32" CXX="clang++ -m32" LDFLAGS="$LDFLAGS,-l:libobjc.so.4.6" ./configure --prefix=/usr --libdir=/usr/lib32 --sysconfdir=/etc/GNUstep \
    --disable-unicodeconstants --with-ffi-include=/usr/lib32/libffi-`pacman -Q lib32-libffi | cut -f2 -d\ |cut -f1 -d-`/include/

  msg2 "Fix permissions (32-bit)..."
  sed -i 's/tar -xf $(TIMEZONE_ARCHIVE);/tar -xf $(TIMEZONE_ARCHIVE);chown -R root:root * ;/' NSTimeZones/Makefile.postamble

  msg2 "Run 'make' (32-bit)..."
  make
}

# check() {
#   cd "$_svnname"
#   make check
#
#   cd "../$_svnname-32"
#   make check
# }

package() {
  # 64-bit install
  cd "$_svnname"
  msg2 "Install (64-bit)..."
  GNUSTEP_CONFIG_FILE="/etc/GNUstep/GNUstep.conf" make DESTDIR="$pkgdir" install

  # 32-bit install
  cd "../$_svnname-32"
  msg2 "Install (32-bit)..."
  GNUSTEP_CONFIG_FILE="/etc/GNUstep/GNUstep32.conf" make DESTDIR="$pkgdir" install
}
