# Maintainer: X0rg

_svnname=gnustep-make
pkgname=$_svnname-multilib-clang-svn
pkgver=37852
pkgrel=3
pkgdesc="The GNUstep make package for multilib, using Clang"
arch=('x86_64')
url="http://www.gnustep.org/"
license=('GPL3')
groups=('gnustep-multilib-clang-svn')
depends=('lib32-llvm')
makedepends=('svn' 'clang' 'gcc-multilib>=4.9.0')
conflicts=('gnustep-make' 'gnustep-make-clang-svn')
provides=('gnustep-make-clang-svn')
options=('!emptydirs')
source=("$_svnname::svn://svn.gna.org/svn/gnustep/tools/make/trunk/"
	'arch32'
	'arch64')
md5sums=('SKIP'
         '2a6ac6a4b3feac708d3272829cfcdc37'
         '0daa9a592d808306193540bda0980673')

pkgver() {
  cd "$_svnname"
  svnversion | tr -d [A-z]
}

prepare() {
  msg2 "Make a clone of $_svnname"
  cp -Rv "$_svnname" "$_svnname-32"

  msg2 "Copy new file system layouts for Arch..."
  cp -v "$srcdir/arch64" "$_svnname/FilesystemLayouts/"
  cp -v "$srcdir/arch32" "$_svnname-32/FilesystemLayouts/"
}

build() {
  # 64-bit build
  cd "$_svnname"
  msg2 "Run 'configure' (64-bit)..."
  CC="clang" CXX="clang++" ./configure --prefix=/usr --sysconfdir=/etc/GNUstep --with-layout=arch64

  # 32-bit build
  cd "../$_svnname-32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  msg2 "Run 'configure' (32-bit)..."
  CC="clang -m32" CXX="clang++ -m32" ./configure --prefix=/usr --libdir=/usr/lib32 --sysconfdir=/etc/GNUstep --with-layout=arch32 --with-config-file=/etc/GNUstep/GNUstep32.conf
}

package() {
  # 64-bit install
  cd "$_svnname"
  msg2 "Install (64-bit)..."
  make DESTDIR="$pkgdir" install
  echo -e "# Added by $pkgname package\nexport PATH=$PATH" >> "$pkgdir/usr/share/GNUstep/Makefiles/GNUstep-reset.sh"

  # 32-bit install
  cd "../$_svnname-32"
  msg2 "Install (32-bit)..."
  make DESTDIR="$pkgdir" install
  echo -e "# Added by $pkgname package\nexport PATH=$PATH" >> "$pkgdir/usr/share/GNUstep32/Makefiles/GNUstep-reset.sh"
}
