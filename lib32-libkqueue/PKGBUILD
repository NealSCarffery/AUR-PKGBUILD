# Maintainer: X0rg
# Contributor: Nicolas Qui√©not < niQo at aur >

_realname=libkqueue
pkgname=lib32-$_realname
pkgver=2.0.3
pkgrel=1
pkgdesc="userspace implementation of the kqueue kernel event notification mechanism found in FreeBSD and other BSD-based (32-bit)"
url="https://github.com/mheily/libkqueue"
arch=('i686' 'x86_64')
license=('BSD')
depends=('lib32-glibc' 'libkqueue')
makedepends=('gcc-multilib')
options=('!libtool')
source=(https://github.com/mheily/$_realname/releases/download/v$pkgver/$_realname-$pkgver.tar.gz)
md5sums=('42d405a26c36f734e2c7d3a936e30abe')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "$srcdir/$_realname-$pkgver"
#  CFLAGS='-fPIC -I./include -I./src/common -Wall -Werror' ./configure --prefix=/usr || return 1
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd "$srcdir/$_realname-$pkgver"
  make DESTDIR="$pkgdir" install

  # Clean up lib32 package
  rm -rf "$pkgdir"/usr/{include,share}
}

