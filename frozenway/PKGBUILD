# Maintainer: X0rg

_realname=FrozenWay
pkgname=frozenway
pkgver=1.6.5
pkgrel=2
pkgdesc="A project that gives those involved the ability to pass through any firewall or proxy"
arch=('i686' 'x86_64')
url="http://www.frozenway.com"
license=('custom')
depends=('qt4' 'lzo')
makedepends=('wget')
backup=(opt/$pkgname/etc/auth.key
	opt/$pkgname/etc/frozenway.crt)

source_i686=("$pkgname-$pkgver-i686.tar.gz::$url/index.php?module=download&step=get_frozenway&version=$pkgver&system=linux_i386")
source_x86_64=("$pkgname-$pkgver-x86_64.tar.gz::$url/index.php?module=download&step=get_frozenway&version=$pkgver&system=linux_x86_64")
md5sums_i686=('465f3d26dac16f9f416359d924ae8fb3')
md5sums_x86_64=('85d117de94ca702287f6b1cb48c6cc3d')

DLAGENTS=('http::/usr/bin/wget --trust-server-names -c -nv --show-progress -O %o') 

package() {
	# Install license
	install -D "$srcdir/$_realname $pkgver/bin/license.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	rm "$srcdir/$_realname $pkgver/bin/license.txt"

	# Put stuff in /opt/frozenway/bin and /opt/frozenway/etc
	install -d "$pkgdir/opt/$pkgname/"{bin,etc}
	install -v "$srcdir/$_realname $pkgver/bin/"{FrozenWay,FrozenWayAskPass,FrozenWayHelper,openvpn} "$pkgdir/opt/$pkgname/bin/"
	install -v "$srcdir/$_realname $pkgver/etc/"* "$pkgdir/opt/$pkgname/etc/"

	# Install executable
	install -d "$pkgdir/usr/bin/"
	ln -srv "$pkgdir/opt/$pkgname/bin/FrozenWay" "$pkgdir/usr/bin/$pkgname"
}
