# Maintainer: X0rg

_gitname=darling
pkgname=$_gitname-git
pkgver=673.4b84279
pkgrel=10
pkgdesc="A Wine-like emulation layer that enables you to run Darwin/OS X apps on Linux"
arch=('i686' 'x86_64')
url="http://www.darlinghq.org"
license=('GPL3')
depends=('cairo' 'libbsd' 'systemd' 'lcms' 'bzip2' 'util-linux' 'ncurses' 'libxrandr' 'ffmpeg' 'libkqueue' 'gcc-libs' 'gnustep-gui-clang-svn' 'gnustep-corebase-clang-svn')
makedepends=('git' 'clang>=3.1' 'nasm' 'yasm')
optdepends=('dmg2dir-git: extract OS X application from .dmg file')
backup=(etc/darling/dylib.conf)
install=darling.install
source=('git://github.com/LubosD/darling.git')
md5sums=('SKIP')

pkgver() {
  cd "$_gitname"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$_gitname"
  msg2 "Clone submodules..."
  git submodule init
  git submodule update

  msg2 "Make 'build' directory..."
  mkdir -pv ./build
}
 
build() {
  cd "$_gitname/build"
  msg2 "Run 'cmake'..."
  CC="clang" CXX="clang++" CXXFLAGS="${CXXFLAGS//-O2}" cmake .. -DCMAKE_INSTALL_PREFIX=/usr -Wno-dev

  msg2 "Run 'make'..."
  make
}

package() {
  cd "$_gitname/build"
  msg2 "Install..."
  make DESTDIR="$pkgdir" install
}
