# Maintainer: X0rg

_gitname=darling
pkgname=$_gitname-git
pkgver=673.4b84279
pkgrel=12
pkgdesc="A Wine-like emulation layer that enables you to run Darwin/OS X apps on Linux"
arch=('i686' 'x86_64')
url="http://www.darlinghq.org"
license=('GPL3')
depends=('libbsd' 'libsystemd' 'openssl' 'gnustep-corebase-clang-svn' )
makedepends=('git' 'clang35' 'nasm' 'libkqueue' 'gnustep-gui-clang-svn')
optdepends=('dmg2dir-git: extract OS X application from .dmg file')
backup=(etc/darling/dylib.conf)
install=darling.install
source=('git://github.com/LubosD/darling.git')
	#'git://github.com/LubosD/darling-mach.git') Not used
md5sums=('SKIP')
	#'SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  #msg2 "Clone submodules..."
  #mv -v "$srcdir/darling-mach" "$srcdir/$_gitname/src/darling-mach"

  msg2 "Make 'build' directory..."
  mkdir -pv "$srcdir/$_gitname/build"
}

build() {
  cd "$srcdir/$_gitname/build"
  msg2 "Run 'cmake'..."
  CC="clang" CXX="clang++" CXXFLAGS="${CXXFLAGS//-O2}" cmake .. -DCMAKE_INSTALL_PREFIX=/usr -Wno-dev

  msg2 "Run 'make'..."
  make
}

package() {
  cd "$srcdir/$_gitname/build"
  msg2 "Install..."
  make DESTDIR="$pkgdir" install
}
